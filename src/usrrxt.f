! subversion Id for THIS file : $Id: usrrxt.f 8506 2016-01-28 10:09:58Z simonc $
! $URL: http://svn-ae/ae/COMMON/stand-alone/atmos_models/socrates/tags/07.18.01/src/usrrxt.f $
!-----------------------------------------------------------------------

      subroutine USRRXT( rxt
     $,                  temp 
     $,                  invariants )
!-----------------------------------------------------------------
!        ... Set the user specified reaction rates
!-----------------------------------------------------------------
      use GRID_DIMS, only : niz
      use CHEM_MODS, only : rxncnt
      use SPECIES_DIMS, only : nfs  ! number of "fixed" species = 2
      use ZGRID, only : pmb
      use PHYS_CST, only : pmb0
      use RXT_NAMES
      use SPC_NAMES

      implicit none

!-----------------------------------------------------------------
!        ... Dummy arguments
!-----------------------------------------------------------------
      real, intent(in) ::   temp(niz)        ! temperature
     $,                     invariants(niz,nfs)   ! invariants density
      real, intent(out) ::  rxt(niz,rxncnt)  ! gas phase rates
      
!-----------------------------------------------------------------
!        ... Local variables
!-----------------------------------------------------------------
      integer  ::  i, k

      real, dimension(niz) :: tp, tinv, ko, kinf, kinv, fc, m

      m(:) = invariants(:,1)
 
      tinv(:)   = 1. / temp(:)
      tp(:)     = 300. * tinv(:)

!-----------------------------------------------------------------
!     	... o + o + m -> o2 + m
!-----------------------------------------------------------------
      rxt(:,rid_hk1)  = 4.23e-28 * temp(:)**(-2)

!-----------------------------------------------------------------
!     	... O + O2 + M --> O3 + M      (JPL2000 updated)
!-----------------------------------------------------------------
      rxt(:,rid_hk2)  = 6.0e-34 * tp(:)**2.4    ! was 2.3

!-----------------------------------------------------------------
!     	... o1d + n2 + m -> n2o + m
!-----------------------------------------------------------------
      rxt(:,rid_hk21)  = 3.5e-37 * tp(:)**.6

!-----------------------------------------------------------------
!     	... NO3 + NO2 + M --> N2O5 + M
!-----------------------------------------------------------------
      ko(:)        = 2.2e-30 * tp(:)**3.9
      kinf(:)      = 1.5e-12 * tp(:)**.7
      call TROE( rxt(1,rid_b12),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
!     	... N2O5 + M --> NO3 + NO2 + M
!-----------------------------------------------------------------
      kinv(:)  = 2.7e-27 * EXP( 11000.*tinv(:) )
      rxt(:,rid_b32) = rxt(:,rid_b12) / kinv(:)

!-----------------------------------------------------------------
!     	... no2 + o + m -> no3 + m
!-----------------------------------------------------------------
      ko(:)        = 9.e-30 * tp(:)**2.
      kinf(:)      = 2.2e-11
      call TROE( rxt(1,rid_b81),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
!     	... no + o + m -> no2 + m
!-----------------------------------------------------------------
      ko(:)        = 9.e-30 * tp(:)**1.5
      kinf(:)      = 3.e-11
      call TROE( rxt(1,rid_b82),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
!     	... clo + no2 + m -> clono2 + m
!-----------------------------------------------------------------
      ko(:)        = 1.8e-31* tp(:)**3.4
      kinf(:)      = 1.5e-11* tp(:)**1.9
      call TROE( rxt(1,rid_d31),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
!     	... cl + no2 + m -> clno2 + m
!-----------------------------------------------------------------
      ko(:)        = 1.8e-31* tp(:)**2.
      kinf(:)      = 1.e-10* tp(:)
      call TROE( rxt(1,rid_d36),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
!     	... clo + clo + m -> cl2o2 + m
!-----------------------------------------------------------------
      ko(:)        = 2.2e-32* tp(:)**3.1
      kinf(:)      = 3.5e-12* tp(:)
      call TROE( rxt(1,rid_d60),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
!     	... cl2o2 + m -> 2*clo + m
!-----------------------------------------------------------------
      kinv(:) = 1.3e-27 * EXP( 8744.*tinv(:) )
      rxt(:,rid_d61) = rxt(:,rid_d60) / kinv(:)

!-----------------------------------------------------------------
!     	... bro + no2 + m -> brono2 + m
!-----------------------------------------------------------------
      ko(:)        = 5.2e-31* tp(:)**3.2
      kinf(:)      = 6.9e-12* tp(:)**2.9
      call TROE( rxt(1,rid_e13),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
!     	... OH + NO2 + M --> HNO3 + M
!-----------------------------------------------------------------
      ko(:)   = 2.6e-30 * tp(:)**3.2
      kinf(:) = 2.4e-11 * tp(:)**1.3
      call TROE( rxt(1,rid_b22),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
!     	... HO2 + NO2 + M --> HNO4 + M
!-----------------------------------------------------------------
      ko(:)   = 1.8e-31 * tp(:)**3.2
      kinf(:) = 4.7e-12 * tp(:)**1.4
      call TROE( rxt(1,rid_b23),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
!     	... HNO4 + M --> HO2 + NO2 + M
!-----------------------------------------------------------------
      kinv(:) = 2.1e-27 * EXP( 10900.*tinv(:) )
      rxt(:,rid_b24) = rxt(:,rid_b23) / kinv(:)

!-----------------------------------------------------------------
!     	... HNO3 + OH --> H2O + NO3
!-----------------------------------------------------------------
      ko(:) = 2.4e-14 * EXP( 460.*tinv(:) )
      kinf(:) = 2.7e-17 * EXP( 2199.*tinv(:) )   ! k2 in JPL2000
      kinv(:) = 6.5e-34 * EXP( 1335.*tinv(:) )   ! k3 in JPL2000
      rxt(:,rid_b27) = ko(:) + kinv(:) / ( 1. + kinv(:)*m(:)/kinf(:) )

!-----------------------------------------------------------------
!     	... CO + OH --> CO2 + H
!-----------------------------------------------------------------
      rxt(:,rid_a36) = 1.5e-13 * ( 1. + 0.6*pmb(:)/pmb0 )

!-----------------------------------------------------------------
!     	... H + O2 + M --> HO2 + M
!-----------------------------------------------------------------
      ko(:)   = 5.7e-32 * tp(:)**1.6
      kinf(:) = 7.5e-11
      call TROE( rxt(1,rid_a1),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
!     	... oh + oh + m -> h2o2 + m
!-----------------------------------------------------------------
      ko(:)   = 6.9e-31 * tp(:)**.8
      kinf(:) = 1.5e-11
      call TROE( rxt(1,rid_a83),
     $           m,
     $           .6,
     $           ko,
     $           kinf )

!-----------------------------------------------------------------
! 	... ch4 + o1d -> ch2o + h + ho2
!-----------------------------------------------------------------
      rxt(:,rid_c1b) = 0.

!-----------------------------------------------------------------
! 	... clo + ho2 _. hcl + o3
!-----------------------------------------------------------------
      rxt(:,rid_d33a) = 0.

      end subroutine USRRXT
